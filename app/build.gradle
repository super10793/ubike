plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

apply from: 'dependencies.gradle'

def secretsPropertiesFile = rootProject.file("secrets.properties")
def secretsProperties = new Properties()
secretsProperties.load(new FileInputStream(secretsPropertiesFile))

android {
    namespace 'com.demo.ubike'
    compileSdk 34

    defaultConfig {
        applicationId "com.demo.ubike"
        minSdk 22
        targetSdk 34
        versionCode 1003
        versionName "1.0.03"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "API_CLIENT_ID_1", secretsProperties['API_CLIENT_ID_1']
        buildConfigField "String", "API_CLIENT_SECRET_1", secretsProperties['API_CLIENT_SECRET_1']
        buildConfigField "String", "API_CLIENT_ID_2", secretsProperties['API_CLIENT_ID_2']
        buildConfigField "String", "API_CLIENT_SECRET_2", secretsProperties['API_CLIENT_SECRET_2']
        buildConfigField "String", "API_CLIENT_ID_3", secretsProperties['API_CLIENT_ID_3']
        buildConfigField "String", "API_CLIENT_SECRET_3", secretsProperties['API_CLIENT_SECRET_3']
        manifestPlaceholders = [
                "mapsApiKey": secretsProperties.getProperty("MAPS_API_KEY")
        ]
    }

    signingConfigs {
        release {
            storeFile file('../keystore/CPKeystore')
            storePassword secretsProperties['KEYSTORE_PASSWORD']
            keyAlias secretsProperties['KEYSTORE_ALIAS']
            keyPassword secretsProperties['KEYSTORE_ALIAS_PASSWORD']
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            manifestPlaceholders = [
                    "appName": "@string/app_name_debug"
            ]
        }
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationIdSuffix ""
            manifestPlaceholders = [
                    "appName": "@string/app_name"
            ]
        }
    }

    sourceSets {
        debug.java.srcDirs += ['src/buildTypes/debug']
        release.java.srcDirs += ['src/buildTypes/release']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    dataBinding {
        enabled = true
    }
    buildFeatures {
        viewBinding true
    }
}

